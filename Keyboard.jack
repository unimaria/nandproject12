class Keyboard{
		
		static int KEYBOARDINPUT;
		static int MAX_STR_LENGTH;

		function void init()
		{
			let KEYBOARDINPUT = 24576;
			let MAX_STR_LENGTH = 20;
			return;
		}

		function char keyPressed()
		{
			var char key;
			let key = Memory.peek(KEYBOARDINPUT);
			return key;
		}

		function char readChar()
		{
			var boolean done;
			var char theChar;
			let done = false;
			while(~done)
			{
				let theChar = Keyboard.keyPressed();

				if(~(theChar = 0))
				{
					let done = true;
					while( ~(0 = Keyboard.keyPressed()) )
					{
						do Sys.wait(1);
					}
					if( ~((theChar = String.backSpace()) | (theChar = String.newLine())) )	//todo make sure we can do these comparisons
					{
						do Output.printChar(theChar);
					}
					return theChar;
				}
				do Sys.wait(1);
			}
			return theChar;
		}

		function String readLine(String message)
		{
			var String theString;
			var char nextChar;

			let theString = String.new(MAX_STR_LENGTH);
			do Output.printString(message);
			let nextChar = 0; 			//todo see if we can do this assignment
			while( ~(nextChar = String.newLine()) )		//todo check that its a good comparison
			{
				let nextChar = Keyboard.readChar();

				if(nextChar = String.newLine())
				{
					do Output.println();
					return theString;
				}	
				if(nextChar = String.backSpace())
				{
					if(theString.length() > 0)
					{
						do theString.eraseLastChar();
						do Output.backSpace();
					}
				}
				else
				{
					do theString.appendChar(nextChar);
				}
			}
			return theString;
		}

		function int readInt(String message)
		{
			//todo check and see if it works
			var String theString;
			var char nextChar;
			var int result;
			let theString = String.new(MAX_STR_LENGTH);
			do Output.printString(message);
			let nextChar = 0; 			//todo see if we can do this assignment
			while(~(nextChar = String.newLine()))		//todo check that its a good comparison
			{
				let nextChar = Keyboard.readChar();

				if(nextChar = String.newLine())
				{
					do Output.println();
					let result = theString.intValue();
					return result;
				}	
				if(nextChar = String.backSpace())
				{
					if(theString.length() > 0)
					{
						do theString.eraseLastChar();
						do Output.backSpace();
					}
				}
				else
				{
					do theString.appendChar(nextChar);
				}
			}
			return result;
		}
}