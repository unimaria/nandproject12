class String{
	
	field Array string;
	field int strMaxLength;
	field int length;
	field int zeroAscii;
	field int nineAscii;
	
	constructor String new(int maxLength){
		let length = 0;
		if (maxLength = 0){
			let maxLength = 1;
			let length = 1;
		}
		let string = Array.new(maxLength); //check: if maxLength = 0?
		let strMaxLength = maxLength;
		let zeroAscii = 48; //a constant that holds ascii for 0 digit
		let nineAscii = 57;
		return this;
	}
	
	method void dispose(){
		do Array.dispose(string);
		return;
	}
	
	method int length(){
		return length;
	}
	
	method char charAt(int j){
		return string[j];
	}
	
	method void setCharAt(int j, char c){
		let string[j] = c;
		return;
	}
	
	method String appendChar(char c){
		if (length < strMaxLength){
			let string[length] = c;
			let length = length + 1;
		}
		return this;
	}
	
	method void eraseLastChar(){
		if (length > 0){
			let length = length - 1;
		}
		return;
	}
	
	method int intValue(){
		var int val;
		var int i;
		var int d;
		
		let val = 0;
		let i = 0;
		// if the firs char is minus
		if (string[0] = 45){
			let i = 1;
		}
		while((i < length) & (string[i] > (zeroAscii - 1)) & (string[i] < (nineAscii + 1))){
			let d = string[i] - zeroAscii;
			let val = (val * 10) + d;
			let i = i + 1;
		}
		if (string[0] = 45){
			let val = -val;
		}
		return val;
	}
	
	method void setInt_helper(int number){
		var int tens;
		var int lastDigit;
		var int c;
		var int newNumber;
	
		let tens = number / 10;
		let tens = tens * 10;
		let lastDigit = number - tens;
		let c = zeroAscii + lastDigit; // char representing lastdigit
		if (number < 10){
			do appendChar(c);
			return;
		}
		else{
			let newNumber = number / 10;
			do setInt_helper(newNumber);
			do appendChar(c);
			return;
		}
	}
	
	method void setInt(int number){
		let length = 0;
		if (number < 0){
			let number = -number;
			do appendChar(45); // ascii for minus
		}
		
		do setInt_helper(number);
		return;
	}
	
	function char newLine(){
		return 128;
	}
	
	function char backSpace(){
		return 129;
	}
	
	function char doubleQuote(){
		return 34;
	}
	
}